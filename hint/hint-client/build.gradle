apply plugin: 'java-library'
apply plugin: 'maven-publish'

bootJar {
	enabled = false
}

jar {
	enabled = true
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
		mavenBom "io.micrometer:micrometer-tracing-bom:${micrometerTracingVersion}"
	}
}

dependencies {
	api project(":hint-model")
	api 'org.springframework.cloud:spring-cloud-starter-openfeign'
	api 'de.signaliduna.elpa:jwt-adapter:2.2.1'
	implementation("com.fasterxml.jackson.core:jackson-annotations:${jacksonVersion}")
	implementation 'jakarta.validation:jakarta.validation-api:3.1.1'


	api 'io.micrometer:micrometer-tracing'
	implementation 'io.github.openfeign:feign-micrometer'

	testImplementation 'org.springframework.boot:spring-boot-starter-web'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.cloud:spring-cloud-contract-wiremock'
}

publishing {
	publications {
		HintClient(MavenPublication) {
			from components.java
		}
	}

	repositories {
		maven {
			name = 'SILocal'
			credentials {
				username project.findProperty('NEXUS_USER') as String ?: ''
				password project.findProperty('NEXUS_PASSWORD') as String ?: ''
			}
			url = (project.version as String).contains("SNAPSHOT") ? 'https://m2repo.system.local/content/repositories/si-snapshot' : 'https://m2repo.system.local/content/repositories/si-release'
			allowInsecureProtocol true
		}
	}
}
